GenerateMonthCalendar =
LAMBDA(YEAR, MONTH,
    LET(month,MONTH,year,YEAR,
        TEXT(DATE(year,month,SEQUENCE(DAY(EOMONTH(DATE(year,month,1),0)),1,1,1)),"DD-MM-YYYY")))

ColumnSummaryReport =
LAMBDA(DataColumn, ColumnName,
    LET(
        Members, UNIQUE(DataColumn),
        MemberCount, COUNTIF(DataColumn, Members),
        ReportBody, CHOOSE({1,2}, Members, MemberCount),
        ReportHeader, CHOOSE({1,2}, ColumnName, "Count of " & ColumnName),
        ReportFooter, CHOOSE({1,2}, "Total", SUM(MemberCount)),
        VSTACK(ReportHeader, ReportBody, ReportFooter)
    )
)

NextReviewYear = 
LAMBDA(RegisteredDate, RiskRating,
  LET(
    obyear, YEAR(RegisteredDate),
    crYear, YEAR(TODAY())-1,
    tenure, crYear - obyear,
    Policy, IFS(
      RiskRating = "HIGH", 1,
      OR(RiskRating = "MEDIUM HIGH", RiskRating = "MEDIUM"), 3,
      OR(RiskRating = "LOW", RiskRating = "NULL"), 5
    ),
    qnt, QUOTIENT(tenure, Policy),
    ReviewYear, (qnt * Policy) + obyear + Policy,
    ReviewYear
  )
)
